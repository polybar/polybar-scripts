#! /usr/bin/env fish

function unmute
    dunstctl set-paused false && \
        dunstify 'dunst' 'Unmuted notifications.' -i polari -u low &
end

function mute
    dunstify 'dunst' 'Muting notifications...' -i polari -u low &
    # We're about to mute notifications, so the above notification will
    # effectively not show, since set-paused hides notifications. So, sleep
    # for 2 seconds.
    sleep 2
    # Close all to prevent notification from showing up once we unmute.
    dunstctl close-all
    dunstctl set-paused true
end

function pop_all
    for id in (dunstctl history | jq -r '.data[][].id.data')
        dunstctl history-pop $id
    end
end

if test (dunstctl is-paused) = "true"
    switch $argv[1]
    case 'toggle'
        unmute
    case 'show-all'
        unmute
        pop_all
    case '*'
        echo ' '(dunstctl count waiting)
    end
else
    switch $argv[1]
    case 'toggle'
        mute
    case 'show-all'
        pop_all
    case '*'
        echo ''
    end
end
